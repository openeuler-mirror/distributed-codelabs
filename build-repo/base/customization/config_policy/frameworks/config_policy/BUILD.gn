# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/customization/config_policy/config_policy.gni")
if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
} else {
  import("//build/ohos.gni")
}

config_policy_sources = [ "src/config_policy_utils.c" ]
config("config_policy_config") {
  include_dirs = [
    "//base/customization/config_policy/interfaces/inner_api/include",
    "//third_party/bounds_checking_function/include",
    "//base/startup/init/services/include/param",
  ]
}

if (defined(ohos_lite) && ohos_kernel_type == "liteos_m") {
  static_library("configpolicy_util") {
    sources = config_policy_sources
    if (config_policy_fs_prefix != "") {
      print("cust config_policy_fs_prefix: ${config_policy_fs_prefix}")
      defines = [ "ROOT_PREFIX=\"${config_policy_fs_prefix}\"" ]
    }
    include_dirs = [
      "//base/customization/config_policy/interfaces/inner_api/include",
      "//third_party/bounds_checking_function/include",
    ]
  }
} else if (defined(ohos_lite)) {
  shared_library("configpolicy_util") {
    sources = config_policy_sources
    public_configs = [ ":config_policy_config" ]
    deps = [ "//third_party/bounds_checking_function:libsec_shared" ]
  }
} else {
  ohos_shared_library("configpolicy_util") {
    sources = config_policy_sources
    public_configs = [ ":config_policy_config" ]
    deps = [
      "etc:customization_etc",
      "//third_party/bounds_checking_function:libsec_shared",
    ]
    external_deps = [ "init:libbegetutil" ]

    subsystem_name = "customization"
    part_name = "config_policy"
  }
  ohos_static_library("configpolicy_util_for_init_static") {
    sources = config_policy_sources
    public_configs = [ ":config_policy_config" ]
    subsystem_name = "customization"
    part_name = "config_policy"
  }
}
