# Copyright (c) 2020 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/lite/config/component/lite_component.gni")
import("//foundation/distributeddatamgr/kv_store/kv_store.gni")

declare_args() {
  enable_ohos_utils_native_lite_kv_store_use_posix_kv_api = true
}

if (ohos_kernel_type == "liteos_m") {
  static_library("utils_kv_store") {
    sources = [
      "${kv_store_native_path}/kv_store/src/kvstore_common/kvstore_common.c",
    ]
    if (enable_ohos_utils_native_lite_kv_store_use_posix_kv_api) {
      sources += [
        "${kv_store_native_path}/kv_store/src/kvstore_impl_posix/kv_store.c",
      ]
    } else {
      sources +=
          [ "${kv_store_native_path}/kv_store/src/kvstore_impl_hal/kv_store.c" ]
    }
    include_dirs = [
      "//commonlibrary/utils_lite/include",
      "${kv_store_native_path}/kv_store/src/kvstore_common",
      "${kv_store_api_path}/kv_store/include",
    ]
  }
} else {
  shared_library("utils_kv_store") {
    cflags = [ "-Wall" ]
    cflags_cc = cflags
    sources = [
      "${kv_store_native_path}/kv_store/src/kvstore_common/kvstore_common.c",
      "${kv_store_native_path}/kv_store/src/kvstore_impl_posix/kv_store.c",
    ]
    include_dirs = [
      "//commonlibrary/utils_lite/include",
      "${kv_store_native_path}/kv_store/src/kvstore_common",
      "//third_party/bounds_checking_function/include",
      "${kv_store_api_path}/kv_store/include",
    ]
    public_deps = [ "//third_party/bounds_checking_function:libsec_shared" ]
  }
}

lite_component("kv_store") {
  features = [ ":utils_kv_store" ]
}
