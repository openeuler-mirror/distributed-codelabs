# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

bus_center_sdk_test_src = [ "bus_center_sdk_test.cpp" ]
softbus_center_sdk_test_src = [ "softbus_center_sdk_test.cpp" ]
bus_center_sdk_test_inc = [
  "$dsoftbus_root_path/adapter/common/include",
  "$dsoftbus_root_path/core/bus_center/interface",
  "$dsoftbus_root_path/core/bus_center/lnn/lane_hub/heartbeat/include",
  "$dsoftbus_root_path/core/bus_center/lnn/lane_hub/lane_manager/include",
  "$dsoftbus_root_path/core/bus_center/lnn/net_ledger/local_ledger/include",
  "$dsoftbus_root_path/core/common/include",
  "$dsoftbus_root_path/core/discovery/interface",
  "$dsoftbus_root_path/core/discovery/manager/include",
  "$dsoftbus_root_path/interfaces/kits/bus_center",
  "$dsoftbus_root_path/interfaces/kits/common",
  "$dsoftbus_root_path/tests/sdk/common/include",
  "//commonlibrary/c_utils/base/include",
  "//third_party/bounds_checking_function/include",
  "$dsoftbus_root_path/sdk/bus_center/manager/include",
]
bus_center_sdk_test_deps = [
  "$dsoftbus_root_path/core/frame:softbus_server",
  "$dsoftbus_root_path/sdk:softbus_client",
  "//base/security/access_token/interfaces/innerkits/nativetoken:libnativetoken",
  "//base/security/access_token/interfaces/innerkits/token_setproc:libtoken_setproc",
  "//third_party/bounds_checking_function:libsec_static",
]

if (dsoftbus_feature_lnn_meta_node) {
  bus_center_sdk_test_src += [ "bus_center_meta_node_sdk_test.cpp" ]
}

if (dsoftbus_feature_lnn_heartbeat) {
  bus_center_sdk_test_src += [ "bus_center_heartbeat_sdk_test.cpp" ]
  bus_center_sdk_test_inc += [
    "$dsoftbus_root_path/core/bus_center/interface",
    "$dsoftbus_root_path/core/bus_center/lnn/lane_hub/heartbeat/include",
    "$dsoftbus_root_path/core/bus_center/lnn/net_ledger/common/include",
  ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//build/lite/config/test.gni")

  if (ohos_build_type == "debug") {
    unittest("BusCenterSdkTest") {
      output_extension = "bin"
      output_dir = "$root_out_dir/test/unittest/dsoftbus"
      sources = bus_center_sdk_test_src
      include_dirs = bus_center_sdk_test_inc
      deps = bus_center_sdk_test_deps
      deps += [ "$hilog_lite_deps_path" ]
    }
  }
  if (ohos_build_type == "debug") {
    unittest("SoftbusCenterSdkTest") {
      output_extension = "bin"
      output_dir = "$root_out_dir/test/unittest/dsoftbus"
      sources = softbus_center_sdk_test_src
      include_dirs = bus_center_sdk_test_inc
      deps = bus_center_sdk_test_deps
      deps += [ "$hilog_lite_deps_path" ]
    }
  }
} else {
  import("//build/test.gni")

  module_output_path = "dsoftbus/bus_center"
  ohos_unittest("BusCenterSdkTest") {
    module_out_path = module_output_path
    sources = bus_center_sdk_test_src
    include_dirs = bus_center_sdk_test_inc
    include_dirs += [
      "unittest/common/",
      "//commonlibrary/c_utils/base/include",
    ]
    deps = bus_center_sdk_test_deps
    deps += [
      "$dsoftbus_root_path/tests/sdk/common:softbus_access_token_test",
      "//third_party/googletest:gtest_main",
    ]
    if (is_standard_system) {
      external_deps = [
        "c_utils:utils",
        "hiviewdfx_hilog_native:libhilog",
      ]
    } else {
      external_deps = [
        "c_utils:utils",
        "hilog:libhilog",
      ]
    }
  }

  ohos_unittest("SoftbusCenterSdkTest") {
    module_out_path = module_output_path
    sources = softbus_center_sdk_test_src
    include_dirs = bus_center_sdk_test_inc
    include_dirs += [
      "unittest/common/",
      "//commonlibrary/c_utils/base/include",
    ]
    deps = bus_center_sdk_test_deps
    deps += [
      "$dsoftbus_root_path/tests/sdk/common:softbus_access_token_test",
      "//third_party/googletest:gtest_main",
    ]
    if (is_standard_system) {
      external_deps = [
        "c_utils:utils",
        "hiviewdfx_hilog_native:libhilog",
      ]
    } else {
      external_deps = [
        "c_utils:utils",
        "hilog:libhilog",
      ]
    }
  }
  group("unittest") {
    testonly = true
    deps = [
      ":BusCenterSdkTest",
      ":SoftbusCenterSdkTest",
    ]
  }
}
